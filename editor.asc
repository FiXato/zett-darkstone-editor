10  'Darkstone (c) freeware 2021
20 ' version 16.27052021
30 '
40 '   Made by Zett Darkstone
50 '
60 '     twitch.tv/zett_darkstone
70 '
80 '   Comments and modifications by FiXato
90 '     https://contact.fixato.org
100 '
110 DEFINT A-Z
120 SCREEN 5,1:D=0:DIM YL(99):DIM XL(99)
130 FOR E=0 TO 3:SET PAGE 0,E:COLOR 15,0,0:CLS:NEXT E:SET PAGE 0,1:TE=0
140 COLOR 15,0,0:COLOR=RESTORE
150 BLOAD"i9_7.sc5",S
160 BLOAD"i9_7.co5",S:COLOR=RESTORE
170 FOR Z=0 TO 63 STEP 16:D=0
180 FOR Y=0 TO 39 STEP  8:D=D+24
190 FOR X=0 TO 79 STEP 16
200 XL(TE)=(127+X+Y-D):YL(TE)=(95-Z)+Y+(X\2)-(C*4):TE=TE+1
210 NEXT X
220 NEXT Y
230 NEXT Z
240 SET PAGE 1,1
250 'Create the selection and placement frame as a sprite
260 SPRITE$(0)=CHR$(  3)+CHR$( 12)+CHR$( 48)+CHR$(192)+CHR$(176)+CHR$(140)+CHR$(131)+CHR$(128)
270 SET PAGE 0,0
280 SPRITE$(0)=CHR$(  3)+CHR$( 12)+CHR$( 48)+CHR$(192)+CHR$(176)+CHR$(140)+CHR$(131)+CHR$(128)
290 X=0:Y=0:Z=0
300 'move cursor on editor
310 D=STICK(0)
320 IF D=1 THEN Y=Y-5:IF Y<0 THEN Y=20
330 IF D=3 THEN X=X+1:IF X>4 THEN X=0
340 IF D=5 THEN Y=Y+5:IF Y>20 THEN Y=0
350 IF D=7 THEN X=X-1:IF X<0 THEN X=4
360 'z-axis movement, tile placement, and tile selector detection.
370 A$=INKEY$
380 IF A$="z" THEN Z=Z+25:IF Z>75  THEN Z=0
390 IF A$="x" THEN Z=Z-25:IF Z<0 THEN Z=75
400 IF A$="c" THEN GOSUB 800
401 IF A$="h" THEN H=1:PUT SPRITE 0,(-32,209),0
402 IF A$="H" THEN H=0
410 IF A$=CHR$(32) THEN GOSUB 1200
420 IF H<>1 THEN PUT SPRITE 0,(XL(X+Y+Z),YL(X+Y+Z)),15-(Z\25),0
440 GOTO 310

800 'Tile Selector subroutine
810 XS=0:YS=0:SET PAGE 1,1
820 D=STICK(0)
830 IF D=1 THEN YS=YS-32:IFYS<0 THEN YS=0
840 IF D=3 THEN XS=XS+32:IFXS>224 THEN XS=0
860 IF D=5 THEN YS=YS+32:IFYS>33 THEN YS=32
870 IF D=7 THEN XS=XS-32:IFXS<0 THEN XS=224
880 ' Put the selection frame sprite on the tile selector screen
881 PUT SPRITE 0,(XS,YS),15,0
882 IF INKEY$=CHR$(32) THEN SET PAGE 0,0:RETURN
890 GOTO 820

900 ' Subroutine to draw tiles on main placement screen
910 IF C=0 THEN RETURN
920 IF C>7 THEN YI=32 : XI=(C-8)*32:ELSE YI=0:XI=C*32
930 COPY (XI,YI)-(31+XI,31+YI),1 TO(XL(XR+YR+ZA),YL(XR+YR+ZA)),0,TPSET:RETURN

1200 ' Tile placement subroutine
1201 ' opslaan nieuwe tile en heropbouw z=hoogte wordt verdeeld in zx en zy
1210 IF Z=0 THEN ZX=0:ZY=0
1211 IF Z=25 THEN ZX=128:ZY=0
1212 IF Z=50 THEN ZX=0:ZY=106
1213 IF Z=75 THEN ZX=128:ZY=106
1220 ' opslaan van nieuwe tile in lagen zx&zy laag xs&ys welke tile
1221 SET PAGE 0,0:CLS:SET PAGE 0,3:PSET(ZX+X,ZY+(Y/5)),((XS/32)+(YS/32)*8)
1230 ' hertekenen van het wat we nu zien xr&yr nieuwe opbouw.
1240 FOR XR=0 TO 4:FOR YR=0 TO 20 STEP 5
1250 ' z1
1260 C=POINT(XR,YR/5):ZA=0
1270 GOSUB 900
1280 ' z2
1290 C=POINT(128+XR,YR/5):ZA=25
1300 GOSUB 900
1310 ' z3
1320 C=POINT(XR,106+YR/5):ZA=50
1330 GOSUB 900
1340 ' z4
1350 C=POINT(128+XR,106+YR/5):ZA=75
1360 GOSUB 900
1370 NEXT YR
1380 NEXT XR
1390 SET PAGE 0,0:RETURN



