10  'Darkstone (c) freeware 2021
20 ' version 24.20210602.0011
30 '
40 '   Made by Zett Darkstone
50 '
60 '     twitch.tv/zett_darkstone
70 '
80 '   Comments and modifications since v19 by FiXato
90 '     https://contact.fixato.org
91 DEFINT A-Z:D=0:DIM YL(99):DIM XL(99):DIM VP(31):TE=0:H=0
100 IF PEEK(&H2D)>2 THEN J=250 ELSE J=50 ' turboR gets a lower cursor sensitivity
101 COLOR 15,0,0:CLS:PRINT "Select tileset:": PRINT "1) Lava": PRINT "2) Fields": PRINT "3) Lakeside":PRINT "4) Faces": PRINT "C) Custom"
102 F$=INKEY$
103 IF F$="1" THEN F$="z_lava":GOTO 110
104 IF F$="2" THEN F$="z_fields":GOTO 110
105 IF F$="3" THEN F$="z_lake1":GOTO 110
106 IF F$="4" THEN F$="f_faces":GOTO 110
107 IF F$="C" OR F$="c" THEN FILES "*.SC5":PRINT CHR$(10):INPUT "What is the filename (without extension)";F$:GOTO 110
109 GOTO 102
110 SCREEN 5,1
130 GOSUB 1650 ' Reset pages
131 SET PAGE 0,1:BLOAD F$+".SC5",S
132 ON ERROR GOTO 133:BLOAD F$+".CO5",S:GOTO 134
133 RESUME 134
134 ON ERROR GOTO 0
135 COLOR=RESTORE
140 GOSUB 1700 ' Store palette in VP
141 GOSUB 1800 ' Setup of coordinates arrays
240 'Create the selection and placement frame as a sprite on both pages
250 SET PAGE 0,1: GOSUB 700
270 SET PAGE 0,0: GOSUB 700
290 X=0:Y=0:Z=0:XO=1:YO=5:XM=4:YM=20:XS=0:YS=0:GOSUB 1875 ' Show placement sprite on canvas
300 FOR I=0 TO J:NEXT I 'Slow down cursor movement
301 A = INSTR("qwertyuiQWERTYUIzZxchH-+S" + CHR$(32) + CHR$(27) + CHR$(28) + CHR$(29) + CHR$(30) + CHR$(31),INPUT$(1))
302 ON A GOSUB 480, 480, 480, 480, 480, 480, 480, 480, 490, 490, 490, 490, 490, 490, 490, 490, 500, 520, 520, 800, 540, 550, 560, 570, 1500, 1200, 2000, 335, 355, 325, 345
303 GOTO 300

320 GOSUB 335 '2: UP+RIGHT
325 Y=Y-YO:GOSUB 360:GOSUB 1875:RETURN '1: UP
330 GOSUB 345 '4: DOWN+RIGHT
335 X=X+XO:GOSUB 370:GOSUB 1875:RETURN '3: RIGHT
340 GOSUB 355 '6: DOWN+LEFT
345 Y=Y+YO:GOSUB 380:GOSUB 1875:RETURN '5: DOWN
350 GOSUB 325 '8: UP+LEFT'
355 X=X-XO:GOSUB 390:GOSUB 1875:RETURN '7: LEFT

359 ' Wrap x and y around their max and min values
360 IF Y<0 THEN Y=YM
361 RETURN
370 IF X>XM THEN X=0
371 RETURN
380 IF Y>YM THEN Y=0
381 RETURN
390 IF X<0 THEN X=XM
391 RETURN

480 YS=0:XS=32*(A-1):GOSUB 1200:RETURN 'q-i'
490 YS=32:XS=32*(A-9):GOSUB 1200:RETURN 'Q-I'

500 Z=Z+25:IF Z>75 THEN Z=0 'z
501 GOSUB 1875:RETURN
520 Z=Z-25:IF Z<0 THEN Z=75 'Z or x
521 GOSUB 1875:RETURN

540 H=1:PUT SPRITE 0,(-32,217),0:RETURN 'h to hide the selection sprite
550 H=0:RETURN 'H to enable the selection sprite again

560 J=J+50:PRINT "J";J:RETURN ' decrease cursor sensitivity
570 J=J-50:IF J<0 THEN J=0 ' increase cursor sensitivity
571 RETURN

700 ' Sprite definition for tile selection and placement
701 SPRITE$(0)=CHR$(  3)+CHR$( 12)+CHR$( 48)+CHR$(192)+CHR$(176)+CHR$(140)+CHR$(131)+CHR$(128)
703 SPRITE$(1)=CHR$(255)+CHR$( 253)+CHR$( 255)+CHR$(255)+CHR$(131)+CHR$(163)+CHR$(163)+CHR$(255)+CHR$(7)
704 RETURN

800 'Tile Selector subroutine
810 XS=0:YS=0:SET PAGE 1,1
820 D=STICK(0)
830 IF D=1 THEN YS=YS-32:IF YS<0 THEN YS=0
840 IF D=3 THEN XS=XS+32:IF XS>224 THEN XS=0
860 IF D=5 THEN YS=YS+32:IF YS>33 THEN YS=32
870 IF D=7 THEN XS=XS-32:IF XS<0 THEN XS=224
871 GOSUB 1850 ' Put the selection frame sprite on the tile selector screen
882 A$=INKEY$
883 IF A$=CHR$(32) THEN SET PAGE 0,0:A$="":RETURN ' Make tile selection
884 IF A$="-" THEN J=J+50 ' cursor sensitivity
885 IF A$="+" THEN J=J-50:IF J<0 THEN J=0 ' cursor sensitivity
889 FOR I=0 TO J:NEXT I 'Slow down cursor movement
890 GOTO 820

900 ' Subroutine to draw tiles on main placement screen
910 IF C=0 THEN RETURN 'ignore what is placed on the first tile so it can be used to blank a tile, even though it might display graphics.
920 IF C>7 THEN YI=32 : XI=(C-8)*32:ELSE YI=0:XI=C*32 ' second row of tiles
930 COPY (XI,YI)-(31+XI,31+YI),1 TO(XL(XR+YR+ZA),YL(XR+YR+ZA)),0,TPSET:RETURN

1200 ' Tile placement subroutine
1201 ' opslaan nieuwe tile en heropbouw z=hoogte wordt verdeeld in zx en zy
1210 IF Z=0 THEN ZX=0:ZY=0
1211 IF Z=25 THEN ZX=128:ZY=0
1212 IF Z=50 THEN ZX=0:ZY=106
1213 IF Z=75 THEN ZX=128:ZY=106
1220 ' opslaan van nieuwe tile in lagen zx&zy laag xs&ys welke tile
1221 SET PAGE 0,0:CLS:SET PAGE 0,3:PSET(ZX+X,ZY+(Y/5)),((XS/32)+(YS/32)*8)
1230 ' hertekenen van het wat we nu zien xr&yr nieuwe opbouw.
1240 FOR XR=0 TO 4:FOR YR=0 TO 20 STEP 5
1250 ' z1
1260 C=POINT(XR,YR/5):ZA=0
1270 GOSUB 900
1280 ' z2
1290 C=POINT(128+XR,YR/5):ZA=25
1300 GOSUB 900
1310 ' z3
1320 C=POINT(XR,106+YR/5):ZA=50
1330 GOSUB 900
1340 ' z4
1350 C=POINT(128+XR,106+YR/5):ZA=75
1360 GOSUB 900
1370 NEXT YR
1380 NEXT XR
1390 SET PAGE 0,0:RETURN

1500 'Subroutine to store the current drawing to a file; .GRP is only the current graphic data; .SC5 is the entire screen.
1501 GOSUB 1600 'Normalise date
1502 GOSUB 1625 'Get time-based file basename
1503 PUT SPRITE 1,(50,50),5,1 ' Show floppy disk sprite as saving indicator
1511 'Strip spaces; thx bsittler: https://www.msx.org/forum/msx-talk/software-and-gaming/disk-menus?page=5
1512 F$="":FOR I=1 TO LEN(D$)
1513 IF MID$(D$,I,1)<>" " THEN F$=F$+MID$(D$,I,1)
1514 NEXT I
1520 FOR I=0 TO 3
1521 'COPY (0,0)-(199,192),0 TO F$+".GRP":
1522 SET PAGE 0,I:GOSUB 1750 ' Restore palette data
1523 IF I=0 THEN BSAVE F$+".SC5",0,&h769f,S ELSE BSAVE F$+".00" + RIGHT$(STR$(I),1),0,&h769f,S
1524 NEXT I:SET PAGE 0,0
1525 PUT SPRITE 1,(-32,217),1 'Hide saving indicator
1526 RETURN

1600 'Subroutine to convert GET DATE to Y/M/D format
1601 GET DATE D$:ON ((PEEK(&H2B) AND &B01110000)/16+1) GOSUB 1603,1604,1605
1602 RETURN 'return from 1600 sub
1603 RETURN ' Y/M/D is already the right format, nothing to change here
1604 D$=MID$(D$,7,2)+"/"+MID$(D$,1,2)+"/"+MID$(D$,4,2):RETURN ' Convert from M/D/Y format
1605 D$=MID$(D$,7,2)+"/"+MID$(D$,4,2)+"/"+MID$(D$,1,2):RETURN ' Convert from D/M/Y format

1625 ' Get time-based file basename from TIME+DATE
1626 'Convert date and time to an 8 digit integer sort of representing amount of minutes since year 00
1627 'obviously it doesn't take the century change into account,
1628 'and it assumes every month is 31 days and every year is 366 days,
1629 'but it should give an incremental integer that fits a filename.
1630 '`F$` will contain this filename without suffix.
1631 GET TIME T$:D$=STR$((VAL(MID$(D$,1,2))*535680!)+(VAL(MID$(D$,4,2))*44640!)+(VAL(MID$(D$,7,2))*1440)+VAL(MID$(T$,1,2))*60+VAL(MID$(T$,4,2))):RETURN


1650 ' Reset pages
1651 FOR I=0 TO 3:SET PAGE 0,I:CLS:COLOR 15,0,0:NEXT I:I=0:RETURN

1700 ' Store current palette in VP
1701 FOR C=0 TO 31
1702 VP(C)=VPEEK(BASE(5*5+3)+128+C)' Use BASE to calculate location of palette in VRAM in Screen 5
1703 NEXT C:C=0:RETURN

1750 ' Restore current palette from VP
1752 FOR C=0 TO 31
1753 VPOKE BASE(5*5+3)+128+C, VP(C)
1754 NEXT C:C=0:RETURN

1800 ' Setup of  coordinates arrays
1801 C=0
1802 FOR Z=0 TO 63 STEP 16:D=0
1803 FOR Y=0 TO 39 STEP  8:D=D+24
1804 FOR X=0 TO 79 STEP 16
1805 XL(TE)=(127+X+Y-D):YL(TE)=(95-Z)+Y+(X\2)-(C*4):TE=TE+1 'TODO: check what this actually does, as C was originally undefined at this point...
1806 NEXT X:NEXT Y:NEXT Z:RETURN

1850 PUT SPRITE 0,(XS,YS),15,0:RETURN ' Put the selection frame sprite on the tile selector screen

1875 IF H<>1 THEN PUT SPRITE 0,(XL(X+Y+Z),YL(X+Y+Z)),15-(Z\25),0:RETURN ELSE RETURN ' Show placement sprite on canvas

2000 SCREEN 0: PRINT "Good Bye":END
