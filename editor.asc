10  'Darkstone (c) freeware 2021
20 ' version 16.27052021
30 '
40 '   Made by Zett Darkstone
50 '
60 '     twitch.tv/zett_darkstone
61 '
62 '   Comments and modifications by FiXato
63 '     https://contact.fixato.org
64 '
70 DEFINT A-Z
80 SCREEN 5,1:D=0:DIM YL(99):DIM XL(99)
90 FOR E=0 TO 3:SET PAGE 0,E:COLOR 15,0,0:CLS:NEXT E:SET PAGE 0,1:TE=0
100 COLOR 15,0,0:COLOR=RESTORE
110 BLOAD"i9_7.sc5",S
120 BLOAD"i9_7.co5",S:COLOR=RESTORE
130 FOR Z=0 TO 63 STEP 16:D=0
140 FOR Y=0 TO 39 STEP  8:D=D+24
150 FOR X=0 TO 79 STEP 16
160 XL(TE)=(127+X+Y-D):YL(TE)=(95-Z)+Y+(X\2)-(C*4):TE=TE+1
170 NEXT X
180 NEXT Y
190 NEXT Z
200 SET PAGE 1,1
209 'Create the selection and placement frame as a sprite
210 SPRITE$(0)=CHR$(  3)+CHR$( 12)+CHR$( 48)+CHR$(192)+CHR$(176)+CHR$(140)+CHR$(131)+CHR$(128)
220 SET PAGE 0,0
230 SPRITE$(0)=CHR$(  3)+CHR$( 12)+CHR$( 48)+CHR$(192)+CHR$(176)+CHR$(140)+CHR$(131)+CHR$(128)
240 X=0:Y=0:Z=0
249 'move cursor on editor
250 D=STICK(0)
260 IF D=1 THEN Y=Y-5:IF Y<0 THEN Y=20
270 IF D=3 THEN X=X+1:IF X>4 THEN X=0
280 IF D=5 THEN Y=Y+5:IF Y>20 THEN Y=0
290 IF D=7 THEN X=X-1:IF X<0 THEN X=4
299 'z-axis movement, tile placement, and tile selector detection.
300 A$=INKEY$
310 IF A$="z" THEN Z=Z+25:IF Z>75  THEN Z=0
320 IF A$="x" THEN Z=Z-25:IF Z<0 THEN Z=75
330 IF A$="c" THEN GOTO 370
340 IF A$=CHR$(32) THEN GOTO 460
349 'Put the placement frame sprite on screen
350 PUT SPRITE 0,(XL(X+Y+Z),YL(X+Y+Z)),15-(Z\25),0
360 GOTO 250
369 'Tile Selector subroutine
370 XS=0:YS=0:SET PAGE 1,1
380 D=STICK(0)
390 IF D=1 THEN YS=YS-32:IFYS<0 THEN YS=0
400 IF D=3 THEN XS=XS+32:IFXS>224 THEN XS=0
410 IF D=5 THEN YS=YS+32:IFYS>33 THEN YS=32
420 IF D=7 THEN XS=XS-32:IFXS<0 THEN XS=224
429 ' Put the selection frame sprite on the tile selector screen
430 PUT SPRITE 0,(XS,YS),15,0
440 IF INKEY$=CHR$(32) THEN SET PAGE 0,0:GOTO 250
450 GOTO 380
460 ' opslaan nieuwe tile en heropbouw z=hoogte wordt verdeeld in zx en zy
470 IF Z=0 THEN ZX=0:ZY=0
480 IF Z=25 THEN ZX=128:ZY=0
490 IF Z=50 THEN ZX=0:ZY=106
500 IF Z=75 THEN ZX=128:ZY=106
510 ' opslaan van nieuwe tile in lagen zx&zy laag xs&ys welke tile
520 SET PAGE 0,0:CLS:SET PAGE 0,3:PSET(ZX+X,ZY+(Y/5)),((XS/32)+(YS/32)*8)
530 ' hertekenen van het wat we nu zien xr&yr nieuwe opbouw.
540 FOR XR=0 TO 4:FOR YR=0 TO 20 STEP 5
550 ' z1
560 C=POINT(XR,YR/5):ZA=0
570 GOSUB 900
600 ' z2
610 C=POINT(128+XR,YR/5):ZA=25
620 GOSUB 900
650 ' z3
660 C=POINT(XR,106+YR/5):ZA=50
670 GOSUB 900
700 ' z4
710 C=POINT(128+XR,106+YR/5):ZA=75
720 GOSUB 900
750 NEXT YR
760 NEXT XR
770 SET PAGE 0,0
780 GOTO 250
900 ' Subroutine to draw tiles on main placement screen
910 IF C=0 THEN RETURN
920 IF C>7 THEN YI=32 : XI=(C-8)*32:ELSE YI=0:XI=C*32
930 COPY (XI,YI)-(31+XI,31+YI),1 TO(XL(XR+YR+ZA),YL(XR+YR+ZA)),0,TPSET:RETURN

